# Maintainer : Nicolas Guillaumin <nicolas plus archlinux at guillaumin dot me>
_perlmod=Device-USB
_modnamespace=Device
pkgname=perl-device-usb
pkgver=0.35
pkgrel=1
pkgdesc="Perl wrapper around the libusb 0.1 library"
arch=(i686 x86_64 armv7h)
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'libusb-compat' 'perl-inline')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
md5sums=(8ce79b0c26336dafd3d29f8cb534a925)

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # libusb_ C functions prefix conflicts with libusb-compat
  # See https://rt.cpan.org/Public/Bug/Display.html?id=65908#txn-1123493
  sed -i s/\\blibusb_/wrap_usb_/g ./*.{pm,pl} ./lib/Device/*.pm ./lib/Device/USB/*.pm ./t/*.t
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make

}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
